plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.4'
    id 'io.spring.dependency-management' version '1.1.7'
    // 1. 문서만들기
    id 'org.asciidoctor.jvm.convert' version '3.3.2'
}

group = 'com.example'
version = '1.0'

java {
    sourceCompatibility = '21'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // 2. 문서만들기
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-mustache'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation group: 'com.auth0', name: 'java-jwt', version: '4.3.0'
    implementation group: 'org.qlrm', name: 'qlrm', version: '4.0.1'
    implementation group: 'org.mindrot', name: 'jbcrypt', version: '0.4'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'com.h2database:h2'
    annotationProcessor 'org.projectlombok:lombok'
    runtimeOnly 'com.mysql:mysql-connector-j'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

// 3. 문서만들기

// 1. gradlew build (test코드 실행 -> 결과 build/generated-snippets에 copy된다. *.adoc) -> 변수 설정
ext {
    set('snippetsDir', file("build/generated-snippets"))
}

// 2. gradlew build (test코드 실행 -> 진짜로 1번의 결과가 실행됨)
// 여기서 진짜 실행
tasks.named('test') {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

// 3. 테스트가 종료되면 asciidoctor 실행 -> *.adoc을 가지고 하나의 .adoc 파일로 변경 (src/docs/asciidoc)
tasks.named('asciidoctor') {
    inputs.dir snippetsDir
    dependsOn test // test가 실행되고 나서 실행됨 - 의존
}

// 4. HTML 파일을 서버에서 열어볼 수 있게 copy
tasks.register('copyRestDocs', Copy) {
    dependsOn tasks.named('asciidoctor')
    from "${asciidoctor.outputDir}"  // asciidoctor 결과 위치
    into "src/main/resources/static/docs" // /static/docs로 복사!
}

tasks.named('bootJar') {
    dependsOn tasks.named('copyRestDocs')
}

// plain 파일 생성하지 않기
jar {
    enabled = false
}